#created on: Dec 30, 2019
package sheeprules

// list any import classes here.
import com.model.Model;
import com.model.Question;
import com.model.Shed;
import com.model.Materials;
import com.model.Business;
import com.model.Land;
import com.model.Care;
import com.model.Sheep;

import com.model.Cost;

import com.model.Main; 
//import function com.model.Utility.help;

dialect "java"

rule "Wants self birth"
	when 
		$care : Care(wantsSelfBirth == SELF); // We should make this true and false
		Cost($sheepSoldEarnings : sheepSoldEarnings);
	then
		$care.setAdvice("Birthing yourself is essential as a herder, otherwise the vet needs to come everytime. You can expect to gain " 
		+ $sheepSoldEarnings + " euros from selling sheep.");
end
 
rule "Hobby, wants other birth"
	when 
		$care : Care(wantsSelfBirth == OTHER);
		Business(hobSemiPro == HOBBY);
	then
		$care.setAdvice("Letting birthing be done is not viable, so you should learn how to do it. Races like the Soay lamb unassisted, " + 
		"so herding those could be considered as well.");
end

rule "Pro, wants other birth"
	when 
		$care : Care(wantsSelfBirth == OTHER);
		Business(hobSemiPro == PRO);
	then
		$care.setAdvice("As a professional it is recommended that you birth yourself. Letting birthing be done is not viable, " + 
		" so you should learn how to do it.");
end

rule "Pro doesn't want lambs"
	when
		$care : Care(wantsLambs == false);
		Business(hobSemiPro == PRO);
		Sheep($nSheep : totalNSheepWanted);
		Cost($sheepSoldEarnings : sheepSoldEarnings);
	then
		$care.setAdvice("As a professional it is recommended that you breed sheep. It is your main earning. Every sheep get roughly 2 lambs " +
		"and sells for 95 euros. Therefore, with " + $nSheep + " you're expected to gain " + $sheepSoldEarnings +
		" euros from selling lambs.");
end

rule "Shave self"
	when 
		$care : Care(wantsSelfShave);
		Cost($shaveOtherCost : shaveOtherCost, $woolEarnings : woolEarnings);
		Sheep($totalNSheepWanted : totalNSheepWanted); 
	then
		$care.setAdvice("Keep in mind that Shaving is not profitable. Per sheep you receive roughly 1.50 euros. In your case you want " + 
		$totalNSheepWanted + " sheep. Therefore, this will earn you about " + $woolEarnings + " euros. ");
		$care.setAdvice("One can also hire someone to do it for them. Per sheep it costs roughly 3.50 euros. Therefore this would cost you around " +
		$shaveOtherCost + " euros.");
end

rule "Shave other"
	when 
		$care : Care(wantsSelfShave == false);
		Cost($shaveOtherCost : shaveOtherCost);
		Sheep($totalNSheepWanted : totalNSheepWanted); 
	then
		$care.setAdvice("Shaving is not profitable. However, shaving your sheep needs to happen roughly one time per year." +
		"One can hire someone to do it for them. In your case you want " + $totalNSheepWanted + ", per sheep shaving costs" + 
		"roughly 3.50 euros. Therefore this will cost you around " + $shaveOtherCost + " euros.");
end

rule "Hobby, shave other"
	when 
		$care : Care(wantsSelfShave == false);
		Business(hobSemiPro == HOBBY);
	then
		$care.setAdvice("As a hobby herder you could choose a race of sheep that doesn't need shaving, like the Soay.");
end

rule "No Slaughter, Enter Competition"
	when 
		$business : Business(hobSemiPro == HOBBY);
		$care : Care(wantsSlaughter == false);
	then
		$care.setAdvice("Since you do not want to slaughter your sheep you might want to be part of a sheep herding competition. You can learn more about it on https://www.dagvanhetschaap.nl/.");
end

rule "Sheep grazing on cow herders land"
	when 
		$business : Business(hobSemiPro == PRO);
		$care : Care();
	then
		$care.setAdvice("It is a good idea to let your sheep graze at the local cow herder land. This strenghtens the "+
		"imunity of your sheep toward illness. While the sheep are grazing elsewhere the sheepherder has the opportunity to work the land and create hay for the sheep for the winter. ");
end




		
		
		