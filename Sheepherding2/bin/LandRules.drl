#created on: Dec 30, 2019
package sheeprules

// list any import classes here.
import com.model.Model;
import com.model.Question;
import com.model.Shed;
import com.model.Materials;
import com.model.Business;
import com.model.Land;
import com.model.Care;
import com.model.Sheep;
import com.model.Cost;


import com.model.Main; 
//import function com.model.Utility.help;

dialect "java"

rule "Land is big enough"
	when
		$land : Land(landIsBigEnough);
	then
		$land.setAdvice("You have enough land for the amount of sheep you would like to hold");
end 

rule "Not enough land"
	when
		Sheep( $nSheep : totalNSheepWanted);
		$land : Land( $landSize : totalLandSize, $landNeeded : landNeeded > 0);		
		Cost(  $costBuyLand : boughtLandNeededCost, $costLeaseLand : landNeededCost);
// Only talking about buying land. How much will leasing land cost rougly?
	then
		//help(drools,"info");
		$land.setAdvice("Buy or lease more land. Per acre you can hold roughly 3 sheep. Since you want " + 
		$nSheep + " sheep, and you have " + $landSize + " acres of land (incl. lease) you need " + $landNeeded + " acres more land.");
		$land.setAdvice("An acre of land costs rougly 50.000 euro. Therefore you'd need " + (int)$costBuyLand + ".- euros for buying land. " + 
		"Therefore it's better to lease land if possible. This will only cost you " + (int)$costLeaseLand + ".- euros." );
end

rule "Not enough land, no lease"
	when
		$land : Land(landNeeded > 0, hasLeasedLand == false);
	then
		$land.setAdvice("Try to increment your available land by checking for leasing options in your area."); 
end     

rule "Hobby, land too small, no shed"
	when
		$land : Land(landNeeded > 0);
		Shed(hasShed == false);
		Shed(isAllowedToBuild == false || roomForShed < goalCurSizeDiff);
		Business(hobSemiPro == HOBBY);
	then 
		$land.setAdvice("You have little land and no shed. You could go for the dutch land goat, which needs little land and doesn't need a shed.");
end

rule "Hobby, has (too) small land"
	when
		$land : Land(hasLand, landNeeded > 0);
		Business(hobSemiPro == HOBBY);
	then
		$land.setAdvice("Since you don't have enough land, instead of buying or leasing more, you could choose sheep that need smaller land " +
		"sizes like the Skudde, Soay, Quessant or English races like the Hampsindown, Suffock and Kerry Hill.");
end

rule "Toeslagrechten non-zero"
	when
		$land : Land($earnings : toeslagrecht > 0); 
		//Cost($earnings : toeslagrechtEarnings);
	then
		$land.setAdvice("For every hectare of used land with entitlements (i.e. toeslagrechten) one receives 350 euro a year. " +
		"In your case this will earn you " + $earnings + " euro");	
end	

rule "Toeslagrechten zero hectares"
	when
		$land : Land(toeslagrecht == 0); 
	then
		$land.setAdvice("For every hectare of used land with entitlements (i.e. toeslagrechten) one recieves 350 euro a year. " +
		"So, when getting more land it is recommended to check wheter it has these, since they cannot be bought anymore.");	
end	

rule "Has Enough land for more sheep"
	when
		$sheep : Sheep($nSheep : totalNSheepWanted);
		$land : Land(landIsBigEnough == true,  totalLandSize -landNeeded >= 0.08 ,$freeLand : totalLandSize -landNeeded, $totalLandSize: totalLandSize); 
	then
		$land.setAdvice("It is best to use your land fully. Since you will have " + $totalLandSize + " hecatars of land you can fit " + $freeLand/0.08 + 
		" more sheep. If you do not want to do that you can always work the land, create haybales for your sheep and sell the excess");	
end	
rule "Too much land needed"
	when
		$land : Land(landNeeded > 10, $landNeeded: landNeeded); 
	then
		$land.setAdvice("You need" + $landNeeded + " hectares. That much amount of land might be difficult to acqure next to your farm. You might want to move the another location" + 
		"where this is possible or care for less sheep ");	
end			
		
		