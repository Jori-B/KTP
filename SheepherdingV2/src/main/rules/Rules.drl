#created on: Dec 7, 2019
package cityrules

// list any import classes here.
import com.sample.Model;
import com.sample.Fact;
import com.sample.Shed;
import com.sample.Materials;
import com.sample.Business;
import com.sample.Land;
import com.sample.Care;
import com.sample.Sheep;
import com.sample.Values;
//import com.sample.ReadInput;
import com.sample.Main; 
import function com.sample.Utility.help;
import function com.sample.ValueCalculations.*;

import function com.sample.Advice.*;

// declare any global variables here

global Values gvalues;

dialect "java"

// Second order questions, which need to be asked using the Fact class. 
rule "Has shed"
	when
		ownsShed : Fact(Fact.Name == "ynShed", answer == YES);
		nmShedSize: Fact(Fact.Name =="nmShedSize", askNow == false); 
	then
		help(drools,"he has a shed");
		modify(nmShedSize) { askQuestion(true) }
end

// Sometimes the user goes back to the previous questions and changes the answer, therefore the rule model needs
// to take into account that this shed size second order question isn't asked then!
rule "Has shed changed"
	when
		ownsShed : Fact(Fact.Name == "ynShed", answer == NO);
		nmShedSize: Fact(Fact.Name =="nmShedSize", askNow == true);
	then
		help(drools,"he has changed having a shed");
		modify(nmShedSize) { setAskNow(false) }
		// MAYBE THE SHEDSIZE ANSWER SHOULD BE SET TO 0 AS WELL???
end

rule "Has land"
	when
	// status is a variable that's not yet used, but I thought it might be useful to have such a variable to prevent rules from firing twice...
		land : Fact(Fact.Name == "ynLand", answer == YES);
		nmLandSize : Fact(Fact.Name == "nmLandSize", askNow == false);
	then
		help(drools,"info");
		modify(nmLandSize) { askQuestion(true) }
		gvalues.addAvailableLand(nmLandSize.getAnswer());	
end

rule "Has land has changed"
	when
		land : Fact(Fact.Name == "ynLand", answer == NO);
		nmLandSize : Fact(Fact.Name == "nmLandSize", askNow == true);
	then
		help(drools,"info");
		modify(nmLandSize) { setAskNow(false) }
end

// Sheep needs to have more salience than land, care and shed so that the functions for that can be used
// numberOfSheepMore needs to be calculated after inserting land (so modify)
rule "All questions asked, sheep calculations"
	// salience is automatically set at 0
	salience 10
	when
		Model(allQuestionsAsked == true, $sheep : getSheep());
	then 
		$sheep.calcTotalSheep();
		$sheep.calcPriceSheep();
		insert( $sheep );
end

rule "All questions asked, land calculations"
	salience 9
	when
		Model(allQuestionsAsked == true, $land : getLand(), $sheep : getSheep());
	then 
		
		$land.calcLandSize();
		$land.calcLandNeeded( $sheep.getTotalNSheepWanted() );
		$land.calcPhosphateRights( $sheep.getTotalNSheepWanted() );
		$land.calcCostLandNeeded();
		insert( $land );
		// I think this modify works, but it needs to be tested
		modify( $sheep ) { calcNumberOfSheepMore($land.getTotalLandSize()) };
end

rule "All questions asked, care calculations"
	salience 8
	when
		Model(allQuestionsAsked == true, $care : getCare(), $shed : getShed());
		Sheep( $nSheep : totalNSheepWanted );
	then 
		$care.calcShaveOtherCost($nSheep);	
		$care.calcWoolEarning($nSheep);
		insert($care);
		
		$shed.calcGoalSize($nSheep);
		$shed.calcGoalCurSizeDiff();
		insert($shed);
end


// When all questions are asked we insert the classes that hold the answers      
rule "All questions asked"
	salience 1
	when
		model : Model(allQuestionsAsked == true);
	then 
		//advice(model.getSheep().getSheepCost());
		help(drools, "all q asked, sheep, land, care, shed inserted");
		insert( model.getBusiness() );
		insert( model.getMaterials() );
end 

rule "No shed"
	when
		Shed( hasShed.answer == NO);
		Sheep( $nSheep : totalNSheepWanted);
	then
		help(drools,"info");
		advice("You should build a shed. Since you want " + $nSheep + 
		" sheep your shed should be " + $nSheep * 10 + " square meters big");
end
 

// To be profitable one needs to have at least 200 sheep and 70 acres of land (SO THIS SHOULD BE A RULE FOR PROFFESIONAL SHEEP HERDERS. For 200 sheep you need 70 acres of land
// 200/70 is 2.86 sheep/acre. 
rule "Has land and wants n sheep"
	when
		Sheep( $nSheep : totalNSheepWanted);
		Land( $landOwned : ownedLandSize.answer, $landNeeded : landNeeded > 0, $cost : costLandNeeded )
		
		
// Only talking about buying land. How much will leasing land cost rougly?
	then
		help(drools,"info");
		advice("Buy or lease more land. Per acre you can hold roughly 3 sheep. Since you want " + 
		$nSheep + " sheep, and you have " + $landOwned + " acres of land, you need " +
		$landNeeded + " acres more land.\n" +
		"An acre of land costs rougly 50.000 euros. Therefore you'll need " + 
		$cost + "euros for buying land.\n" + 
		"Also one needs to keep in mind that 300 euros entitlement (i.e. toeslagrechten) need to be paid per used acre of land. \n" +
		"In your case this will cost you " + toeslagrechten($nSheep,$landOwned) + " euros");
		//Add values
		//gvalues.addSheep(sheep.getAnswer());
		//gvalues.addNeededLand(numberOfAcresMore(sheep.getAnswer(), nmLandSize.getAnswer()));
		//gvalues.addMoneyCost(neededMoneyForLand(sheep.getAnswer(), nmLandSize.getAnswer()));
		//gvalues.addMoneyCost(toeslagrechten(sheep.getAnswer(),nmLandSize.getAnswer()));
		
end

rule "Has enough land and wants n sheep"
	when
		Sheep( $nSheep : totalNSheepWanted, $nSheepMore : nSheepMore );
		Land( $landOwned : ownedLandSize.answer, landNeeded < 0);
	then
		help(drools,"info");
		advice("You have enough land. Per acre you can hold roughly 3 sheep. Since you have\n" + 
		$landOwned + " acres, you could get " + $nSheepMore  + " more sheep than the current\n" + $nSheep + " sheep you wanted");
end

// When you have more than 5 sheep you need to pay phosphate rights. Prices per sheep differ depending on slaugter, male, female, place and weight (non-slaugther lambs are free). Avg is roughly 90ï¿½ per sheep. HOW OFTEN??
rule "Phosphate rights"
	when
		// set $nSheep variable to the total sheep wanted answer if that answer is bigger than 5
		Sheep( $nSheep : totalNSheepWanted > 5 );
		Land( $cost : costPhosphateRights);
	then
		help(drools, "phosphate");  
		advice("When you have more than five sheep you'll need to pay (90 euros) phosphate rights per sheep. For " 
		+ $nSheep + " sheep this will cost " + $cost + " euros per ??");
end

// multiplying the amount of sheep times the shed size is arbitrary. Only during birthing (some) sheep need to be inside
rule "Has too small shed and wants n sheep"
	when
		Sheep( $nSheep : totalNSheepWanted);
		Shed(hasShed.answer == YES, shedTooSmall, $shedSize : curShedSize.answer, $goalCurDiff : goalCurSizeDiff); 
								// shed too small works (no == true is needed)
	then
		help(drools,"info");
		advice("You should build another shed or expand your shed. Since you want " + $nSheep + " sheep, and your shed is\n" 
		+ $shedSize + " square meters, your shed should be " + $goalCurDiff + " square meters bigger");
end

// Why should people have a tractor? What are the alternatives? How much does a tractor cost exactly (https://edepot.wur.nl/429700)? 
rule "Has tractor"
	when 
		tractor : Materials(hasTractor.answer == NO);
	then
		advice("You should probably buy a tractor");

end

// In the end here there should probably be something of an overall advice. Total costs, is it a good idea to start this, when will you start making money, should you lease or buy land, etc.
rule "All good"
	when
		tractor : Materials(hasTractor.answer == YES);
		sheep : Sheep(totalNSheepWanted > 200);
		Land(landIsBigEnough);
		not Shed(shedTooSmall);
	then
		advice("You should be able to start a profitable sheep herding business");
end



		
		
		